locals {
  iam = {
    effect = {
      allow = "Allow"
    }
  }
}

# ================================================================
# Assume Role Policy Document
# ================================================================

data "aws_iam_policy_document" "assume_role_policy_event_bridge" {
  policy_id = "assume_role_policy_event_bridge"
  statement {
    sid     = "AssumeRolePolicyEventBridge"
    effect  = local.iam.effect.allow
    actions = ["sts:AssumeRole"]
    principals {
      identifiers = ["events.amazonaws.com"]
      type        = "Service"
    }
  }
}

data "aws_iam_policy_document" "assume_role_policy_lambda" {
  policy_id = "assume_role_policy_lambda"
  statement {
    sid     = "AssumeRolePolicyLambda"
    effect  = local.iam.effect.allow
    actions = ["sts:AssumeRole"]
    principals {
      identifiers = ["lambda.amazonaws.com"]
      type        = "Service"
    }
  }
}

# ================================================================
# Policy EventBridge Put Events
# ================================================================

data "aws_iam_policy_document" "policy_event_bridge_put_events" {
  policy_id = "policy_event_bridge_put_events"
  statement {
    sid       = "AllowEventBridgePutEvents"
    effect    = local.iam.effect.allow
    actions   = ["events:PutEvents"]
    resources = ["*"]
  }
}

resource "aws_iam_policy" "event_bridge_put_events" {
  policy = data.aws_iam_policy_document.policy_event_bridge_put_events.json
}

# ================================================================
# Policy EventBridge Invoke API Destination
# ================================================================

data "aws_iam_policy_document" "policy_event_bridge_invoke_api_destination" {
  policy_id = "policy_event_bridge_invoke_api_destination"
  statement {
    sid       = "PolicyEventBridgeInvokeApiDestination"
    effect    = local.iam.effect.allow
    actions   = ["events:InvokeApiDestination"]
    resources = ["*"]
  }
}

resource "aws_iam_policy" "event_bridge_invoke_api_destination" {
  policy = data.aws_iam_policy_document.policy_event_bridge_invoke_api_destination.json
}

# ================================================================
# Policy DynamoDB Put Item
# ================================================================

data "aws_iam_policy_document" "policy_dynamodb_put_item" {
  policy_id = "policy_dynamodb_put_item"
  statement {
    sid       = "PolicyDynamodbPutItem"
    effect    = local.iam.effect.allow
    actions   = ["dynamodb:PutItem"]
    resources = [aws_dynamodb_table.tmp.arn]
  }
}

resource "aws_iam_policy" "policy_dynamodb_put_item" {
  policy = data.aws_iam_policy_document.policy_dynamodb_put_item.json
}

# ================================================================
# Policy DynamoDB Get Item
# ================================================================

data "aws_iam_policy_document" "policy_dynamodb_get_item" {
  policy_id = "policy_dynamodb_get_item"
  statement {
    sid       = "PolicyDynamodbGetItem"
    effect    = local.iam.effect.allow
    actions   = ["dynamodb:GetItem"]
    resources = [aws_dynamodb_table.tmp.arn]
  }
}

resource "aws_iam_policy" "policy_dynamodb_get_item" {
  policy = data.aws_iam_policy_document.policy_dynamodb_get_item.json
}

# ================================================================
# Policy KMS Decrypt
# ================================================================

data "aws_iam_policy_document" "policy_kms_decrypt" {
  policy_id = "policy_kms_decrypt"
  statement {
    sid       = "PolicyKmsDecrypt"
    effect    = local.iam.effect.allow
    actions   = ["kms:Decrypt"]
    resources = ["*"]
  }
}

resource "aws_iam_policy" "policy_kms_decrypt" {
  policy = data.aws_iam_policy_document.policy_kms_decrypt.json
}

# ================================================================
# Role Lambda Error Processor
# ================================================================

resource "aws_iam_role" "lambda_error_processor" {
  assume_role_policy = data.aws_iam_policy_document.assume_role_policy_lambda.json
}

resource "aws_iam_role_policy_attachment" "lambda_error_processor" {
  for_each = {
    a = "arn:aws:iam::aws:policy/service-role/AWSLambdaBasicExecutionRole"
    b = aws_iam_policy.event_bridge_put_events.arn
  }
  policy_arn = each.value
  role       = aws_iam_role.lambda_error_processor.name
}

# ================================================================
# Role EventBridge Invoke API Destination
# ================================================================

resource "aws_iam_role" "event_bridge_invoke_api_destination" {
  assume_role_policy = data.aws_iam_policy_document.assume_role_policy_event_bridge.json
}

resource "aws_iam_role_policy_attachment" "event_bridge_invoke_api_destination" {
  for_each = {
    a = aws_iam_policy.event_bridge_invoke_api_destination.arn
  }
  policy_arn = each.value
  role       = aws_iam_role.event_bridge_invoke_api_destination.name
}

# ================================================================
# Role Lambda URL Functions
# ================================================================

resource "aws_iam_role" "lambda_url_functions" {
  assume_role_policy = data.aws_iam_policy_document.assume_role_policy_lambda.json
}

resource "aws_iam_role_policy_attachment" "lambda_url_functions" {
  for_each = {
    a = "arn:aws:iam::aws:policy/service-role/AWSLambdaBasicExecutionRole"
    b = aws_iam_policy.policy_dynamodb_put_item.arn
  }
  policy_arn = each.value
  role       = aws_iam_role.lambda_url_functions.name
}

# ================================================================
# Role Lambda NocoBase Inserter
# ================================================================

resource "aws_iam_role" "lambda_nocobase_inserter" {
  assume_role_policy = data.aws_iam_policy_document.assume_role_policy_lambda.json
}

resource "aws_iam_role_policy_attachment" "lambda_nocobase_inserter" {
  for_each = {
    a = "arn:aws:iam::aws:policy/service-role/AWSLambdaDynamoDBExecutionRole"
    b = "arn:aws:iam::aws:policy/AmazonSSMReadOnlyAccess"
    c = aws_iam_policy.policy_dynamodb_get_item.arn
    d = aws_iam_policy.policy_kms_decrypt.arn
  }
  policy_arn = each.value
  role       = aws_iam_role.lambda_nocobase_inserter.name
}
